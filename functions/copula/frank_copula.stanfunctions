/** @addtogroup frank Frank Copula Functions
 * The Frank copula is defined as 
 * \f[
 *      C(u,v) = -\frac{1}{\omega}\ln \big[1 + \frac{(\exp(-\omega u) - 1)(\exp(-\omega v)-1)}{\exp(-\omega) -1} \big]
 * \f]
 * for \f$\omega \in (-\infty, \infty) \ {0}\f$. 
 * 
 * \ingroup copula
 *  @{ */

/** 
 * Frank Bivariate Copula Log Density
 *
 * The probability density function is defined as
 * \f[
 *     c(u, v) = \frac{ \partial^2 C(u, v) }{ \partial u \partial v} = \frac{-\omega (\exp(-\omega) - 1)(\exp(-\omega(u + v)))}{[(\exp(-\omega u) - 1)\exp(-\omega v) - 1) + \exp(-\omega) - 1]^2}
 * \f]
 *
 * Code: [frank_copula.stanfunctions](https://github.com/spinkney/helpful_stan_functions/blob/main/functions/copula/frank_copula.stanfunctions) \n
 *
 * Copyright Andre Pfeuffer, Sean Pinkney \n
 * https://groups.google.com/g/stan-users/c/hnUtkMYlLhQ/m/XdX3u1vDAAAJ \n
 * Accessed and modified Feb. 5, 2021 
 *
 * The copula is defined \f$0 < \theta < \infty\f$
 *
 * @param u Real number on \f$(0,1]\f$, not checked but function will return NaN
 * @param v Real number on \f$(0,1]\f$, not checked but function will return NaN
 * @param theta Real number on (0, Inf)
 * @return log density
 * @throws reject if theta \f$\le 0\f$
 */
real frank_copula_lpdf(real u, real v, real theta) {
  return log(theta) + log1m(exp(-theta)) - theta * (u + v)
         - 2
           * log(1 - exp(-theta)
                 - (1 - exp(-theta * u)) * (1 - exp(-theta * v)));
}

/** @} */